//    cwshopbot
//    Copyright (C) 2018  Marat Bukharov.
//
//    This program is free software: you can redistribute it and/or modify
//    it under the terms of the GNU Affero General Public License as published by
//    the Free Software Foundation, either version 3 of the License, or
//    (at your option) any later version.
//
//    This program is distributed in the hope that it will be useful,
//    but WITHOUT ANY WARRANTY; without even the implied warranty of
//    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//    GNU Affero General Public License for more details.
//
//    You should have received a copy of the GNU Affero General Public License
//    along with this program.  If not, see <http://www.gnu.org/licenses/>.
buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "com.google.protobuf:protobuf-gradle-plugin:${protobufGradlePluginVersion}"
    }
}

plugins {
    id 'com.github.spotbugs' version '1.6.5'
    id 'net.researchgate.release' version '2.6.0'
}

group 'name.maratik.cw'
version = version

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'https://s3-us-west-2.amazonaws.com/dynamodb-local/release'
    }
    maven {
        url 'https://jitpack.io'
    }
}

apply plugin: 'antlr'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'com.github.spotbugs'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'net.researchgate.release'
apply plugin: 'com.google.protobuf'

sourceCompatibility = 1.8

wrapper {
    gradleVersion = '4.10.2'
    distributionType = 'ALL'
}

configurations {
    compile.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    compile.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    compile.exclude group: 'com.vaadin.external.google', module: 'android-json'
}

dependencies {
    antlr "org.antlr:antlr4:${antlr4Version}"
    compile "org.antlr:antlr4-runtime:${antlr4Version}"
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-log4j2'
    compile 'org.springframework.boot:spring-boot-starter-jetty'
    compile 'org.springframework.boot:spring-boot-starter-amqp'
    compile "org.telegram:telegrambots:${telegrambotsVersion}"
    compile "com.google.guava:guava:${guavaVersion}"
    compile "com.amazonaws:aws-java-sdk-dynamodb:${awsJavaSdkVersion}"
    compile 'org.apache.logging.log4j:log4j-jcl'
    compile 'org.apache.logging.log4j:log4j-jul'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
    compile 'com.fasterxml.jackson.module:jackson-module-afterburner'
    compile "org.apache.lucene:lucene-core:${luceneVersion}"
    compile "org.apache.lucene:lucene-analyzers-common:${luceneVersion}"
    compile "org.apache.lucene:lucene-queryparser:${luceneVersion}"
    compile "com.github.maratik123:spring-telegram:${springTelegramVersion}"
    compile "com.google.protobuf:protobuf-java:${protobufJavaVersion}"

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile "com.amazonaws:DynamoDBLocal:${awsDynamodbLocalVersion}"
    testCompile 'org.springframework.amqp:spring-rabbit-junit'
    testCompile 'org.springframework.amqp:spring-rabbit-test'
}

bootJar {
    launchScript()
}

bootRun {
    jvmArgs = [
            '-Djava.util.logging.manager=org.apache.logging.log4j.jul.LogManager',
            '-Xms128m',
            '-Xmx384m',
            '-server'
    ]
}

test {
    systemProperty 'java.library.path', file('lib').absolutePath
}

spotbugs {
    toolVersion = '3.1.8'
}

generateGrammarSource {
    maxHeapSize = '64m'
    arguments += [
            '-long-messages',
            '-lib', 'src/main/antlr/name/maratik/cw/cwshopbot/parser/generated'
    ]
}
protobuf {
    generatedFilesBaseDir = project.file("generated").absolutePath
    protoc {
        artifact = "com.google.protobuf:protoc:${protocVersion}"
    }
}

clean.delete << protobuf.generatedFilesBaseDir

idea {
    module {
        sourceDirs += generateGrammarSource.outputDirectory
        sourceDirs += project.file("${protobuf.generatedFilesBaseDir}/main/java")
    }
}
